version '1.0-SNAPSHOT'

apply plugin: 'java'


repositories {
    mavenCentral()
}

if (JavaVersion.current().isJava7()) {
    throw new GradleException("This example requires Java 8, but you are using Java 7. Please see the Java 7 example in ../GradleJava7Example")
}

configurations {
    checkerFrameworkAnnotatedJDK {
       description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
    }
    checkerFramework {
       description = 'The Checker Framework: custom pluggable types for Java'
    } 
}

dependencies {
    ext.checkerFrameworkVersion = '1.9.11'
    checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:${checkerFrameworkVersion}"
    checkerFramework "org.checkerframework:checker:${checkerFrameworkVersion}"
    compile "org.checkerframework:checker-qual:${checkerFrameworkVersion}"

    compile 'org.apache.commons:commons-lang3:3.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

allprojects {
    tasks.withType(JavaCompile).all { JavaCompile compile ->
        compile.options.debug = true
        compile.options.compilerArgs = [
                // uncomment to turn Checker Framework errors into warnings
                // '-Awarns',
                '-processorpath', "${configurations.checkerFramework.asPath}",
                '-processor', 'org.checkerframework.checker.nullness.NullnessChecker',
                "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
        ]
    }
}



